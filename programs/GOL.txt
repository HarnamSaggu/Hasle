main(args) {
    x = 38
    y = 25
    board = arr2d(x, y)
    
    # GLIDER ===================================
    glider = {{0, 0, 1}, {1, 0, 1}, {0, 1, 1}}
    transpose(board, glider, 1, 1)
    
    # RANDOM ===========================
    # i = 0
    # while (i < y) {
    #     j = 0
    #     while (j < x) {
    #         board[i][j] = rand() > 0.8
    #         j++
    #     }
    #     i++
    # }
    
    continue = 1
    while (continue) {
        print2d(board)
        next = ~board.next()
        continue = next != board
        board = next
        
        sleep(800)
    }
}

fun transpose(base, img, offX, offY) {
    i = 0
    while (i < img.len()) {
        if (i + offY < base.len()) {
            j = 0
            while (j < img[i].len()) {
                if (j + offX < base[i + offX].len()) {
                    base[i + offY][j + offX] = img[i][j]
                }
                j++
            }
        }
        i++
    }
    <- base
}

fun next(board) {
    y = board.len()
    x = board[0].len()
    next = arr2d(x, y)
    i = 0
    while (i < y) {
        j = 0
        while (j < x) {
            n = 0
            
            offX = -1
            while (offX <= 1) {
                offY = -1
                while (offY <= 1) {
                    if (!(offX == 0 && offY == 0)) {
                        newY = i + offY
                        newX = j + offX
                        if (newY >= 0 && newY < y && newX >= 0 && newX < x) {
                            n += board[newY][newX]
                        }
                    }
                    
                    offY++
                }
                offX++
            }
            
            next[i][j] = n == 3 || (board[i][j] && n == 2)
            
            j++
        }
        i++
    }
    <- next
}

fun arr2d(x, y) {
    arr = {}[y]
    i = 0
    while (i < y) {
        arr[i] = {}[x]
        i++
    }
    <- arr
}

fun print2d(arr) {
    y = arr.len()
    x = arr[0].len()
    i = 0
    println("+", "---".rep(x), "+")
    while (i < y) {
        j = 0
        print("|")
        while (j < x) {
            if (arr[i][j]) {
                print("███")
            } else {
                print("   ")
            }
            j++
        }
        println("|")
        i++
    }
    println("+", "---".rep(x), "+")
}

fun rep(x, count) {
    str = ""
    i = 0
    while (i < count) {
        str += x
        i++
    }
    <- str
}
